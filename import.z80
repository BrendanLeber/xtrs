;; import.z
;; Timothy Mann, 8/24/97
;; Last modified on Mon Aug 25 15:56:53 PDT 1997 by mann
;;
;; Use xtrs 1.4 emulator traps to copy a file from Unix to TRS-80
;; Usage: IMPORT unixfile tofile (NL)
;; If the (NL) parameter is given, each newline ('\n') in the Unix
;;  file is converted to a carriage return ('\r'), the TRS-80 end of
;;  line character.

@fspec  equ 441ch
@init   equ 4420h
@param  equ 4476h
@param3 equ 4454h
@logot  equ 447bh
@logot3 equ 428ah
@write  equ 4439h
@close  equ 4428h
@error  equ 4409h
@exit   equ 402dh
@abort  equ 4030h       

model   equ 0125h ;'I' here => M3

; Emulator trap instructions, byte-reversed for use in defw:
open	equ 30edh
close	equ 31edh
read	equ 32edh
write	equ 33edh
lseek	equ 34edh
strerror equ 35edh

iobsize	equ 8192		; must be divisible by 256

	org 5200h
import: ld a, (model)           ; model 1 or 3?
        cp 'I'
        jr nz, model1           ; go if 1
        push hl
        ld hl, param+1
        ld de, @param3
        ld (hl), e
        inc hl
        ld (hl), d
        ld hl, logot+1
        ld de, @logot3
        ld (hl), e
        inc hl
        ld (hl), d
        pop hl
model1: 
skipsp:	ld a, (hl)              ; scan over Unix filename
        cp ' '		        ; first skip spaces
	jr c, usage             ; error if line ends here
        jr nz, unix1
        inc hl
	jr skipsp
unix1:  ld (uname+1), hl        ; save pointer
unix2:	inc hl                  ; now scan for end of Unix name
        ld a, (hl)
	cp ' '
	jr c, usage
        jr nz, unix2
	ld (hl), 0              ; NUL terminate
        inc hl
	ld de, dcb              ; ready to get LDOS filename from (HL)
        call @fspec
        jr nz, usage
        push hl                 ; save position in command line
        ld hl, iobuf
        ld de, dcb
        ld b, 0
        call @init              ; open the file
        pop hl
        jr z, openok
        call @error
        jp @abort
openok: ld de, prmtbl           ; ready to parse command line
        call param
        jr z, prmok             ; go if OK
usage:  ld hl, usager           ; error message and exit
        call logot
        jp @abort
prmok:  
uname:	ld hl, $-$              ; path
        ld bc, 0                ; oflag (O_RDONLY)
        ld de, 0                ; mode (ignored)
        defw open
        jr z, opn2ok            ; go if OK
        ld hl, uopner           ; error message and exit
	jr uerror

;; Read
rloop:
opn2ok:	ld hl, iobuf            ; read a buffer
	ld bc, iobsize
	defw read
	jr z, readok
        ld hl, urder            ; read error (!!code in A)
	jr uerror
readok:	push de			; save fd

;; Translate
        ld a, (nlparam)		; check for NL feature
	and a
	jr z, nlfals
	ld hl, iobuf
	push bc			; save byte count
	ld a, 0ah
	ld d, 0dh
	inc c			; deal with b=0 and/ c=0
	inc b
	jr tstrt
tloop:	cp (hl)
	jr nz, notcr
	ld (hl), d
notcr:	inc hl
tstrt:	dec c
	jr nz, tloop
	djnz tloop
	pop bc			; restore byte count

;; Write
nlfals:	push bc			; save byte count
	ld hl, iobuf
	ld de, dcb
	inc b			; deal with b=0 and/or c=0
	ld a, c
	and a
	jr z, wstrt
wloop:  ld (dcb+3), hl
	call @write             ; write 256 bytes to file
        jr z, wrok
        call @error             ; oops, i/o error
        jp @abort
wrok:	inc h
wstrt:  djnz wloop
	pop bc			; restore byte count

;; Done?
	pop de			; restore fd
	ld a, c
	and a
	jr nz, closit		; done for sure
	cp b
	jr nz, rloop		; maybe not done (sloppy)

closit:	defw close		; close Unix file
	jr z, closok
        ld hl, uclser           ; close error (!!code in A)
	jr uerror
closok:	ld a, c
	ld (dcb+8), a		; EOF offset
	ld hl, (dcb+10)		; record number (in case shortening file)
	ld (dcb+12), hl
	ld de, dcb
        call @close             ; close the TRS-80 file
        jr z, cls2ok
        call @error             ; oops, i/o error
        jp @abort
cls2ok: jp @exit                ; all is well

;; Unix error, msg in hl, errno in a
uerror: push af
	call logot
	pop af
	ld hl, iobuf
	ld bc, 256
	defw strerror
	call logot
        jp @abort

logot:  jp @logot               ; jump vectors, modified for M3
param:  jp @param

prmtbl: 
        defb 'NL    '
        defw nlparam
        defb 0    

nlparam:
	defw 0

usager: defb 'Usage: IMPORT unixfile tofile (NL=y/n)', 0dh
uopner:	defb 'Error in Unix open: ', 03h
urder:	defb 'Error in Unix read: ', 03h
uclser:	defb 'Error in Unix close: ', 03h

dcb:   defs 32   
iobuf: defs iobsize
        
        end import
